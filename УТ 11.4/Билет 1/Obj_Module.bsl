

Функция СведенияОВнешнейОбработке() Экспорт 
	
	ПараметрыРегистрации = Новый Структура;
	
	МассивНазначений = Новый Массив;
	
	МассивНазначений.Добавить("Документ.РеализацияТоваровУслуг");
	
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", "ABC Классификация по Весу");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь); 
	ПараметрыРегистрации.Вставить("Версия", "1.3");
	ПараметрыРегистрации.Вставить("Информация", "Используется для отобаржения отчета ABC Классификации");
	
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	
	ДобавитьКоманду(ТаблицаКоманд,"ABC Классификация по Весу", "ABCКлассификацияПоВесу",
	"ВызовСерверногоМетода", Истина, "ПечатьXML");
	
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции 

Функция ПолучитьТаблицуКоманд() 
	
	Команды = Новый ТаблицаЗначений;
	
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));    	
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));	
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево")); 	
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));	 	
	
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление,	Идентификатор,Использование,
	ПоказыватьОповещение = Ложь, Модификатор = " ")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор =  Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры 

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм,	ОбъектПечати,ПараметрыВывода) Экспорт 
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ABCКлассификацияПоВесу", "ABC Классификация по Весу",
	СформироватьПечатнуюФормуЧерезСКД(МассивОбъектов,ОбъектПечати)); 
	
КонецПроцедуры  

Функция СформироватьПечатнуюФормуЧерезСКД(МассивОбъектов,ОбъектПечати)
	
	Ссылка = МассивОбъектов[0];
	
	ТабДок = Новый ТабличныйДокумент;
	
	НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
	
	СхемаКомпоновкиДанных = ПолучитьМакет("Макет");
	
	// Получение дефолтных Настроек СКД      
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;	
	КомпоновщикНастроек  = Новый КомпоновщикНастроекКомпоновкиДанных();		
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	
	// задаем Значение Параметра по нашему Документу		
	ПараметрДок = Новый ПараметрКомпоновкиДанных("Ссылка");
	ЗначениеПараметрДок =
		КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрДок);
	ЗначениеПараметрДок.Значение = Ссылка;
	ЗначениеПараметрДок.Использование = Истина;
	Настройки = КомпоновщикНастроек.Настройки;		
	
	// получаем Макет из СКД & заполняем Результатом   		
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,Настройки);
	
	// в ТабДок выводим Документ из СКД 
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);		
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабДок);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.АвтоМасштаб = Истина;
	
	// Меню "Комплект Документов" 
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектПечати, Ссылка);		
	ТабДок.ВывестиВертикальныйРазделительСтраниц();
	
	Возврат ТабДок;
	
КонецФункции 